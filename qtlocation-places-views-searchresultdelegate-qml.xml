<?xml version="1.0" encoding="UTF-8"?>
<db:article xmlns:db="http://docbook.org/ns/docbook" xmlns:xlink="http://www.w3.org/1999/xlink" version="5.2" xml:lang="en">
<db:info>
<db:title>Places (QML)</db:title>
<db:productname>QtLocation</db:productname>
<db:edition>Qt 5.7.1 Reference Documentation</db:edition>
<db:titleabbrev>Qt Location Reference Documentation</db:titleabbrev>
<db:abstract>
<db:para>The Places example demonstrates how to search for Places and access related content.</db:para>
</db:abstract>
<db:pubdate>2025-06-29</db:pubdate>
<db:date>2025-06-29</db:date>
<db:authorgroup>
<db:author>
<db:orgname class="corporation">The Qt Company Ltd.</db:orgname>
</db:author>
</db:authorgroup>
</db:info>
<db:programlisting language="qml">/****************************************************************************
**
** Copyright (C) 2015 The Qt Company Ltd.
** Contact: http://www.qt.io/licensing/
**
** This file is part of the examples of the Qt Toolkit.
**
** $QT_BEGIN_LICENSE:BSD$
** You may use this file under the terms of the BSD license as follows:
**
** &quot;Redistribution and use in source and binary forms, with or without
** modification, are permitted provided that the following conditions are
** met:
**   * Redistributions of source code must retain the above copyright
**     notice, this list of conditions and the following disclaimer.
**   * Redistributions in binary form must reproduce the above copyright
**     notice, this list of conditions and the following disclaimer in
**     the documentation and/or other materials provided with the
**     distribution.
**   * Neither the name of The Qt Company Ltd nor the names of its
**     contributors may be used to endorse or promote products derived
**     from this software without specific prior written permission.
**
**
** THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
** &quot;AS IS&quot; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
** LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
** A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
** OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
** SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
** LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
** DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
** THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
** (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
** OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.&quot;
**
** $QT_END_LICENSE$
**
****************************************************************************/

import QtQuick 2.5
import QtLocation 5.6
import QtQuick.Controls 1.4
import QtQuick.Layouts 1.2
import &quot;../helper.js&quot; as Helper

Item {
    id: root

    signal showPlaceDetails(variant place,variant distance)
    signal searchFor(string query)

    width: parent.width
    height: childrenRect.height

    Component {
        id: placeComponent
        Item {
            id: placeRoot
            width: root.width
            height: Math.max(icon.height, 3 * placeName.height)

            Rectangle {
                anchors.fill: parent
                color: &quot;#44ffffff&quot;
                visible: mouse.pressed
            }

            Rectangle {
                anchors.fill: parent
                color: &quot;#dbffde&quot;
                visible: model.sponsored !== undefined ? model.sponsored : false

                Label {
                    text: qsTr(&quot;Sponsored result&quot;)
                    horizontalAlignment: Text.AlignRight
                    anchors.right: parent.right
                    anchors.bottom: parent.bottom
                    font.pixelSize: 8
                    visible: model.sponsored !== undefined ? model.sponsored : false
                }
            }

            GridLayout {
                rows: 2
                columns: 2
                anchors.fill: parent
                anchors.leftMargin: 30
                flow: GridLayout.TopToBottom

                Image {
                    // anchors.verticalCenter: parent.verticalCenter
                    id:icon
                    source: place.favorite ? &quot;../../resources/star.png&quot; : place.icon.url()
                    Layout.rowSpan: 2
                }

                Label {
                    id: placeName
                    text: place.favorite ? place.favorite.name : place.name
                    Layout.fillWidth: true
                }

                Label {
                    id: distanceText
                    font.italic: true
                    text: Helper.formatDistance(distance)
                    Layout.fillWidth: true
                }
            }

            Rectangle {
                anchors.left: parent.left
                anchors.right: parent.right
                anchors.margins: 15
                height: 1
                color: &quot;#46a2da&quot;
            }

            MouseArea {
                id: mouse
                anchors.fill: parent
                onClicked: {
                    if (model.type === undefined || type === PlaceSearchModel.PlaceResult) {
                        if (!place.detailsFetched)
                            place.getDetails();
                        root.showPlaceDetails(model.place, model.distance);
                    }
                }
            }
        }
    }

    Component {
        id: proposedSearchComponent

        Item {
            id: proposedSearchRoot

            width: root.width
            height: Math.max(icon.height, 2 * proposedSearchTitle.height)

            Rectangle {
                anchors.fill: parent
                color: &quot;#11ffffff&quot;
                visible: mouse.pressed
            }

            RowLayout {
                anchors.fill: parent
                anchors.leftMargin: 30

                Image {
                    source: icon.url()
                }

                Label {
                    id: proposedSearchTitle
                    anchors.verticalCenter: parent.verticalCenter
                    text: &quot;Search for &quot; + title
                }
            }

            Rectangle {
                anchors.left: parent.left
                anchors.right: parent.right
                anchors.margins: 15
                height: 1
                color: &quot;#46a2da&quot;
            }

            MouseArea {
                anchors.fill: parent
                onClicked: root.ListView.view.model.updateWith(index);
            }
        }
    }

    Loader {
        anchors.left: parent.left
        anchors.right: parent.right

        sourceComponent: {
            switch (model.type) {
            case PlaceSearchModel.PlaceResult:
                return placeComponent;
            case PlaceSearchModel.ProposedSearchResult:
                return proposedSearchComponent;
            default:
                //do nothing, don't assign component if result type not recognized
            }
        }
    }
}

</db:programlisting>
</db:article>
