<?xml version="1.0" encoding="UTF-8"?>
<db:article xmlns:db="http://docbook.org/ns/docbook" xmlns:xlink="http://www.w3.org/1999/xlink" version="5.2" xml:lang="en">
<db:info>
<db:title>Enginio QML Examples - Social Todos</db:title>
<db:productname>QtEnginioQml</db:productname>
<db:edition>Qt 5.6.3 Reference Documentation</db:edition>
<db:titleabbrev>Client library for Enginio from QML</db:titleabbrev>
<db:abstract>
<db:para>Social Todos is a todo list application with a social twist, demonstrating the user management and access control features of Enginio.</db:para>
</db:abstract>
<db:pubdate>2025-06-29</db:pubdate>
<db:date>2025-06-29</db:date>
<db:authorgroup>
<db:author>
<db:orgname class="corporation">The Qt Company Ltd.</db:orgname>
</db:author>
</db:authorgroup>
</db:info>
<db:programlisting language="qml">/****************************************************************************
**
** Copyright (C) 2015 The Qt Company Ltd.
** Contact: http://www.qt.io/licensing/
**
** This file is part of the QtEnginio module of the Qt Toolkit.
**
** $QT_BEGIN_LICENSE:BSD$
** You may use this file under the terms of the BSD license as follows:
**
** &quot;Redistribution and use in source and binary forms, with or without
** modification, are permitted provided that the following conditions are
** met:
**   * Redistributions of source code must retain the above copyright
**     notice, this list of conditions and the following disclaimer.
**   * Redistributions in binary form must reproduce the above copyright
**     notice, this list of conditions and the following disclaimer in
**     the documentation and/or other materials provided with the
**     distribution.
**   * Neither the name of The Qt Company Ltd nor the names of its
**     contributors may be used to endorse or promote products derived
**     from this software without specific prior written permission.
**
**
** THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
** &quot;AS IS&quot; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
** LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
** A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
** OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
** SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
** LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
** DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
** THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
** (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
** OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.&quot;
**
** $QT_END_LICENSE$
**
****************************************************************************/

import QtQuick 2.0
import Enginio 1.0

Rectangle {
    id: root
    width: 400
    height: 640
    color: &quot;#f4f4f4&quot;

    property string listId
    property string listName
    property string listCreator

    EnginioModel {
        id: enginioModel
        client: enginioClient
        query: {&quot;objectType&quot;: &quot;objects.todos&quot;,
                &quot;query&quot;: { &quot;todoList&quot;: { &quot;id&quot;: listId, &quot;objectType&quot;: &quot;objects.todoLists&quot; } }
        }
    }

    Header {
        id: header
        text: listName
        rightMargin: icon.width

        Item {
            id: icon
            height: parent.height
            width: parent.height
            anchors.right: parent.right
            Image {
                id: shareButton
                anchors.centerIn: parent
                source: mouse.pressed ? &quot;qrc:icons/share_icon_pressed.png&quot; : &quot;qrc:icons/share_icon.png&quot;
                width:  implicitWidth * scaleFactor
                height: implicitHeight * scaleFactor
                MouseArea {
                    id: mouse
                    anchors.fill: parent
                    onClicked: mainView.push({ item: shareItem, properties: { listId: listId, listName: listName }} )
                }
            }
        }
    }

    Component {
        id: shareItem
        ShareDialog {}
    }

    ListView {
        id: listview
        model: enginioModel
        delegate: listItemDelegate
        anchors.top: header.bottom
        anchors.bottom: footer.top
        width: parent.width
        clip: true

        // Animations
        add: Transition { NumberAnimation { properties: &quot;y&quot;; from: root.height; duration: 250 } }
        removeDisplaced: Transition { NumberAnimation { properties: &quot;y&quot;; duration: 150 } }
        remove: Transition { NumberAnimation { property: &quot;opacity&quot;; to: 0; duration: 150 } }
    }

    BorderImage {
        id: footer

        height: 70 * scaleFactor
        width: parent.width
        anchors.bottom: parent.bottom
        source: &quot;qrc:images/delegate.png&quot;
        border.left: 5; border.top: 5
        border.right: 5; border.bottom: 5

        Rectangle {
            y: -1 ; height: 1
            width: parent.width
            color: &quot;#bbb&quot;
        }
        Rectangle {
            y: 0 ; height: 1
            width: parent.width
            color: &quot;white&quot;
        }

        TextField {
            id: textInput
            anchors.verticalCenter: parent.verticalCenter
            anchors.left: parent.left
            anchors.right: addButton.left
            anchors.margins: 16 * scaleFactor
            placeholderText: &quot;New todo...&quot;
            onAccepted: {
                enginioModel.append({&quot;title&quot;: textInput.text, &quot;done&quot;: false, &quot;todoList&quot;: { &quot;id&quot;: listId, &quot;objectType&quot;: &quot;objects.todoLists&quot; } } )
                textInput.text = &quot;&quot;
            }
        }

        Item {
            id: addButton

            width: 40 * scaleFactor
            height: 40 * scaleFactor
            anchors.margins: 20 * scaleFactor
            anchors.right: parent.right
            anchors.verticalCenter: parent.verticalCenter
            enabled: textInput.text.length
            Image {
                width:  implicitWidth * scaleFactor
                height: implicitHeight * scaleFactor
                source: addMouseArea.pressed ? &quot;qrc:icons/add_icon_pressed.png&quot; : &quot;qrc:icons/add_icon.png&quot;
                anchors.centerIn: parent
                opacity: enabled ? 1 : 0.5
            }
            MouseArea {
                id: addMouseArea
                anchors.fill: parent
                onClicked: textInput.accepted()
            }
        }
    }

    Component {
        id: listItemDelegate

        BorderImage {
            id: item

            width: parent.width ; height: 70 * scaleFactor
            source: mouse.pressed ? &quot;qrc:images/delegate_pressed.png&quot; : &quot;qrc:images/delegate.png&quot;
            border.left: 5; border.top: 5
            border.right: 5; border.bottom: 5

            Image {
                id: shadow
                anchors.top: parent.bottom
                width: parent.width
                visible: !mouse.pressed
                source: &quot;qrc:images/shadow.png&quot;
            }

            MouseArea {
                id: mouse
                anchors.fill: parent
                hoverEnabled: true
                onClicked: {
                    if (index !== -1 &amp;&amp; _synced) {
                        enginioModel.setProperty(index, &quot;done&quot;, !done)
                    }
                }
            }
            Image {
                id: checkbox
                anchors.left: parent.left
                anchors.leftMargin: 16 * scaleFactor
                height: width
                width: 32 * scaleFactor
                fillMode: Image.PreserveAspectFit
                anchors.verticalCenter: parent.verticalCenter
                source: done ? &quot;qrc:images/checkmark.png&quot; : &quot;&quot;
            }

            Text {
                id: todoText
                text: title
                font.pixelSize: 26 * scaleFactor
                color: &quot;#333&quot;

                anchors.verticalCenter: parent.verticalCenter
                anchors.left: checkbox.right
                anchors.right: parent.right
                anchors.leftMargin: 16 * scaleFactor
                anchors.rightMargin: 40 * scaleFactor
                elide: Text.ElideRight
            }

            // Show a delete button when the mouse is over the delegate
            Image {
                id: removeIcon

                source: removeMouseArea.pressed ? &quot;qrc:icons/delete_icon_pressed.png&quot; : &quot;qrc:icons/delete_icon.png&quot;
                width:  implicitWidth * scaleFactor
                height: implicitHeight * scaleFactor
                anchors.margins: 20 * scaleFactor
                anchors.verticalCenter: parent.verticalCenter
                anchors.right: parent.right
                opacity: enabled ? 1 : 0.5
                Behavior on opacity {NumberAnimation{duration: 100}}
                MouseArea {
                    id: removeMouseArea
                    anchors.fill: parent
                    onClicked: enginioModel.remove(index)
                }
            }
        }
    }
}

</db:programlisting>
</db:article>
