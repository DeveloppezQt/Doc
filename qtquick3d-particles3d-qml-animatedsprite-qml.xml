<?xml version="1.0" encoding="UTF-8"?>
<db:article xmlns:db="http://docbook.org/ns/docbook" xmlns:xlink="http://www.w3.org/1999/xlink" version="5.2" xml:lang="en">
<db:info>
<db:title>Qt Quick 3D - Particles 3D Testbed Example</db:title>
<db:productname>QtQuick3D</db:productname>
<db:edition>Qt Quick 3D | Commercial or GPLv3</db:edition>
<db:titleabbrev>Qt Quick 3D Reference Documentation</db:titleabbrev>
<db:abstract>
<db:para>Demonstrates how to use the Qt Quick 3D Particles3D module.</db:para>
</db:abstract>
<db:pubdate>2024-02-14</db:pubdate>
<db:date>2024-02-14</db:date>
<db:authorgroup>
<db:author>
<db:orgname class="corporation">The Qt Company Ltd.</db:orgname>
</db:author>
</db:authorgroup>
</db:info>
<db:programlisting language="qml">// Copyright (C) 2021 The Qt Company Ltd.
// SPDX-License-Identifier: LicenseRef-Qt-Commercial OR BSD-3-Clause

import QtQuick
import QtQuick3D
import QtQuick3D.Particles3D

Item {
    id: mainWindow

    anchors.fill: parent

    View3D {
        anchors.fill: parent

        environment: SceneEnvironment {
            clearColor: &quot;#202020&quot;
            backgroundMode: SceneEnvironment.Color
            antialiasingMode: settings.antialiasingMode
            antialiasingQuality: settings.antialiasingQuality
        }

        PerspectiveCamera {
            id: camera
            position: Qt.vector3d(0, 100, 600)
        }

        ParticleSystem3D {
            id: psystem

            // Particles
            SpriteParticle3D {
                id: animated
                sprite: Texture {
                    source: &quot;images/bear_black.png&quot;
                }
                spriteSequence: SpriteSequence3D {
                    frameCount: 13
                    interpolate: false
                    duration: 2000
                    durationVariation: 1500
                }
                maxAmount: 4
                billboard: true
                blendMode: SpriteParticle3D.Screen
            }

            SpriteParticle3D {
                id: blended
                sprite: Texture {
                    source: &quot;images/explosion_01_strip13.png&quot;
                }
                spriteSequence: SpriteSequence3D {
                    frameCount: 13
                    interpolate: true
                }
                maxAmount: 26
                billboard: true
                blendMode: SpriteParticle3D.Screen
            }

            SpriteParticle3D {
                id: numberParticle
                sprite: Texture {
                    source: &quot;images/sprite_09.png&quot;
                }
                spriteSequence: SpriteSequence3D {
                    id: numberParticleSequence
                    frameCount: 10
                    frameIndex: sliderFrameIndex.sliderValue
                    animationDirection: sliderAnimationDirection.sliderValue
                    interpolate: checkBoxInterpolate.checked
                    randomStart: checkBoxRandom.checked
                    duration: sliderDuration.sliderValue
                    durationVariation: sliderDurationVariation.sliderValue
                }
                maxAmount: 10
                billboard: true
                blendMode: SpriteParticle3D.Screen
            }

            // Emitters, one per particle
            ParticleEmitter3D {
                id: emitter1
                particle: animated
                particleScale: 10
                position: Qt.vector3d(-300, 0, 0)
                emitRate: 1
                lifeSpan: 4000

                shape: ParticleShape3D {
                    fill: true
                }
                Node {
                    y: -140
                    x: -100
                    Text {
                        anchors.verticalCenter: parent.verticalCenter
                        text: &quot;Animated + durationVariation&quot;
                        font.pointSize: settings.fontSizeLarge
                        color: &quot;#ffffff&quot;
                    }
                }
            }

            ParticleEmitter3D {
                id: emitter2
                particle: blended
                position: Qt.vector3d(300, -100, 0)
                particleScale: 20
                velocity: VectorDirection3D {
                    direction: Qt.vector3d(0, 100, 0)
                    directionVariation: Qt.vector3d(50, 50, 50)
                }

                emitRate: 12
                lifeSpan: 2000
                lifeSpanVariation: 200
                Node {
                    y: -40
                    x: -100
                    Text {
                        anchors.verticalCenter: parent.verticalCenter
                        text: &quot;Animated + interpolated&quot;
                        font.pointSize: settings.fontSizeLarge
                        color: &quot;#ffffff&quot;
                    }
                }
            }

            ParticleEmitter3D {
                id: emitter3
                particle: numberParticle
                particleScale: 8
                position: Qt.vector3d(0, 350, 0)
                emitRate: 2
                lifeSpan: 5000
                velocity: VectorDirection3D {
                    direction: Qt.vector3d(0, -100, 0)
                }
                Node {
                    y: 40
                    x: -50
                    Text {
                        anchors.verticalCenter: parent.verticalCenter
                        text: &quot;Controlled&quot;
                        font.pointSize: settings.fontSizeLarge
                        color: &quot;#ffffff&quot;
                    }
                }
            }
        }
    }

    SettingsView {
        CustomLabel {
            property var enumStrings: [&quot;Normal&quot;, &quot;Reverse&quot;, &quot;Alternate&quot;, &quot;AlternateReverse&quot;, &quot;SingleFrame&quot;]
            text: &quot;Animation Direction: &quot; + enumStrings[numberParticleSequence.animationDirection]
        }
        CustomSlider {
            id: sliderAnimationDirection
            sliderValue: 0
            fromValue: 0
            toValue: 4
            sliderStepSize: 1
        }
        CustomLabel {
            text: &quot;Duration&quot;
        }
        CustomSlider {
            id: sliderDuration
            sliderValue: -1
            fromValue: -1
            toValue: 8000
        }
        CustomLabel {
            text: &quot;Duration Variation&quot;
        }
        CustomSlider {
            id: sliderDurationVariation
            sliderValue: 0
            fromValue: 0
            toValue: 2000
        }
        CustomCheckBox {
            id: checkBoxInterpolate
            text: &quot;Interpolate&quot;
            checked: false
        }
        CustomCheckBox {
            id: checkBoxRandom
            text: &quot;Random Start&quot;
            checked: false
        }
        CustomLabel {
            text: &quot;Frame Index&quot;
            opacity: checkBoxRandom.checked ? 0.4 : 1.0
        }
        CustomSlider {
            id: sliderFrameIndex
            sliderValue: 0
            fromValue: 0
            toValue: 9
            sliderEnabled: !checkBoxRandom.checked
            sliderStepSize: 1
        }
    }

    LoggingView {
        anchors.bottom: parent.bottom
        particleSystems: [psystem]
    }
}

</db:programlisting>
</db:article>
